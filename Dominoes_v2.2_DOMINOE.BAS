10 CLEAR1000:DEFINT A-S,U-Z
20 POKE16553,255
30 POKE15423,106:W=PEEK(15423):CLS
40 L$="SQRnSQRfFREDEFSNGSQR~AUTO(wy[FE] FRE[FE]ENDANDCLOSEERR}CE"
50 IFW=106THENPOKE16414,Y:POKE16415,Z:ELSEPOKE16414,88:POKE16415,4
60 CLS
70 DIMD(60),P(60),C(60):N=6:RANDOM
80 PRINT@384,"  DOMINOES       Version 2.2    (C)1979 J.W.B.Dunn."
90 PRINT:PRINT" Written By J.W.B.Dunn       18/5/79  Modified  7/11/80
SOLE DISTRIBUTORS: A.J.HARDING (MOLIMERX).TEL:(0424) 220391
100 IFDM=1THENDM=0:GOTO130
110 PRINT@640,"Generating graphics ";:IFW<>106THENPRINT" ( THIS PROGRAM SUPPORTS LOWER CASE )"
120 GOSUB3530
130 NN$="":A$=""
140 PRINT@640,"                                                         "
150 PRINT"What is your name (FOR COMPUTER -V- COMPUTER WAIT 40 SECS)
";
160 IFT=700THENDM=1:GOTO400:ELSEA$=INKEY$:T=T+1:IFT/5=INT(T/5)THENPRINTCHR$(14);:ELSEPRINTCHR$(15);
170 IFA$=""THEN160:ELSEIFA$=CHR$(8)ANDNN$=""THEN160:ELSEIFA$=CHR$(8)THENPRINTA$;:NN$=LEFT$(NN$,LEN(NN$)-1):GOTO160
180 IFA$=CHR$(13)ANDNN$=""THENPRINT@640,"Impossible":GOTO150
190 IFA$=CHR$(13)THEN220
200 PRINTA$;:NN$=NN$+A$
210 GOTO160
220 PRINTCHR$(15);
230 IFLEN(NN$)>10THENPRINT:PRINT"Too long please abreviate";:GOTO130
240 PRINT@640,"            ";
250 PRINT:PRINT"Do you require instructions ";
260 A$=INKEY$:T=T+1:IFT/5=INT(T/5)THENPRINTCHR$(14);:ELSEPRINTCHR$(15);
270 IFA$="N"THEN PRINT"No.";CHR$(15):FORT=1TO200:NEXTT:GOTO400 :ELSEIFA$="Y"THENPRINT"Yes.";CHR$(15):FORT=1TO200:NEXTT:CLS:GOTO290
280 GOTO260
290 CLS:PRINTCHR$(23)
300 PRINT"     ***  DOMINOES  ***Your dominoes are displayed at
the bottom of the screen.
The dominoes that have been 
played are displayed at the top of the screen.
To knock enter :-  'KK'   .
To play enter the dominoe you
wish to play in the following"
310 PRINT"order :-
the side you wish to play on
first, then the second half  of your dominoe.
Answer questions with Y or N."
320 PRINT"PRESS ANY KEY TO CONTINUE **";
330 A$=INKEY$
340 A$=INKEY$:T=T+1:IFT/5=INT(T/5)THENPRINTCHR$(14);:ELSEPRINTCHR$(15);
350 IFA$=""THEN340
360 CLS:PRINTCHR$(23):PRINT"The game starts with double 6.
If no one has double 6 then play starts with the next double
Eg. 5-4-3-2-1-0.
The first one to play all there dominoes WINS, or if you are
both knocking then the player
with the lowest count of the"
370 PRINT"dominoes left in there hand WINSIf you wish to see the Computersdominoes press 'D' in response
to 'PLAY AGAIN?'
The computers dominoes are
displayed above your dominoes.
PRESS ANY KEY TO PLAY **";
380 A$=INKEY$:T=T+1:IFT/5=INT(T/5)THENPRINTCHR$(14);:ELSEPRINTCHR$(15);
390 IFA$=""THEN380
400 PRINTCHR$(15);:CLS:NG=0:W1=0:W2=0:W3=0:T1=0:T2=0:X1=0:X2=0
410 IFA$="D"THENVG=1:GOTO480
420 VG=0
430 PRINTCHR$(15);
440 IFDM=1THENNN$="Fred":VG=1
450 GOTO480
460 REM        INITIALIZE THE PACK                     **
470 NG=NG+1:GOSUB3310
480 II=0:IX=0:IY=0:IZ=0:LP=28:RP=36:IQ=0:AP=0:IQ=0
490 PRINTCHR$(15);
500 PA=0:X=0 : N=6 : K=0 : CLS
510 CC=0 : PC=0
520 PRINT@0,"Shuffling";
530 FOR A=0TO6
540 FOR B=ATO6
550 IFDM=1THENA$=INKEY$:IFA$<>""THEN4220
560 X=X+1
570 D(X)=A : D(X+28)=B
580 PRINT@12,A$(RND(7)-1);B$(RND(7)-1)
590 NEXT B
600 NEXT A
610 REM        INITIALIZE SCREEN POINTER               **
620 Q=0 : LP=28 :RP=36 :X=0 :Y=0
630 REM        RESET PILE PLAYER KNOCKED ON            **
640 FORT=0TO6
650 PK(T)=9
660 NEXT T
670 PA=0
680 REM        GIVE 7 DOMINOES EACH                    **
690 FOR A=1TO7
700 X=RND(28)
710 IFDM=1THENA$=INKEY$:IFA$<>""THEN4220
720 PRINT@12,A$(RND(7)-1);B$(RND(7)-1)
730 IF D(X)=9 THEN 700
740 P(A)=D(X) : P(A+28)=D(X+28) :D(X)=9 : D(X+28)=9
750 X=RND(28)
760 IFDM=1THENA$=INKEY$:IFA$<>""THEN4220
770 PRINT@12,A$(RND(7)-1);B$(RND(7)-1)
780 IF D(X)=9 THEN 750
790 C(A)=D(X) : C(A+28)=D(X+28) : D(X)=9 : D(X+28)=9
800 NEXT A
810 GOSUB 2960
820 CLS
830 GOSUB 2750
840 GOSUB 1750
850 K=0
860 REM        COMP. CHECKS FOR A DOUBLE TO START      **
870 OUT255,0
880 FOR A=1TO7
890 IFDM=1THENA$=INKEY$:IFA$<>""THEN4220
900 IF C(A)=C(A+28) AND C(A)=N THEN PRINT@0,"                                   ":GOTO1830
910 NEXT A
920 IFDM=1THEN990
930 OUT255,32:PRINT@0,"     I don't have double";N;"do you	 ";
940 OUT255,0:A$=INKEY$ :T=T+1:IFT/5=INT(T/5)THENPRINTCHR$(14);:ELSEPRINTCHR$(15);
950 IFA$=""THEN940
960 IF A$="N" THENPRINTCHR$(15);:GOTO 1080
970 IF A$<>"Y" THEN940
980 PRINTCHR$(15);
990 REM        CHECK IF PLAYER DOES HAVE A DOUBLE      **
1000 FOR A=1TO7
1010 IFDM=1THENA$=INKEY$:IFA$<>""THEN4220
1020 IF P(A)=P(A+28) AND P(A)=N THEN PRINT@0,"                                      ":GOTO1150
1030 NEXT A
1040 IFDM=1THEN1110
1050 OUT255,32
1060 PRINT@0,"CHEAT !   You do not have double";N;"         "
1070 FOR T=1TO500 : NEXT T : GOTO 930
1080 FORA=1TO7
1090 IFP(A)=P(A+28)ANDP(A)=NTHENPRINT@0,"CHEAT ! ! You do have double ";N;"        ":OUT255,32:FORT=1TO500:NEXTT:GOTO930
1100 NEXTA
1110 N=N-1
1120 IFN>=0THEN860
1130 PRINT@0,"No one has a double                         "
1140 FOR T=1TO500 : NEXT T : GOTO 480
1150 REM        PLAYER GOES FIRST                       **
1160 L=P(A) : R=P(A+28) : P(A)=9 : P(A+28)=9
1170 E1=L :E2=R
1180 GOSUB3170
1190 REM        CHECK IF PLAYER WINS                    **
1200 FOR A=1TO7
1210 IF P(A)<>9 THEN 2140
1220 NEXT A
1230 REM        PLAYER WINS                             **
1240 GOSUB 1750
1250 VG=1
1260 GOSUB2750
1270 FORA=1TO7
1280 IFC(A)=9THEN1300
1290 CC=CC+(C(A)+C(A+28))
1300 NEXTA
1310 W2=W2+1
1320 PRINT@267,"You win        Play again ?";:GOTO 2010
1330 REM       PLAYERS TURN                            **
1340 IFDM=1THEN4150
1350 PA=0
1360 GOSUB 2750:PRINT@768,NN$;"'s dominoes ";:IFVG=1THENPRINTCHR$(92);"        ";CHR$(91);" Computer's dominoes";
1370 OUT255,32
1380 PRINT@203,"                              ";
1390 PRINT@203,"Your turn ";
1400 A$="":DB=0:T=0
1410 OUT255,0
1420 B$=INKEY$:T=T+1:IFT/5=INT(T/5)THENPRINTCHR$(14);:ELSEPRINTCHR$(15);
1430 IFB$=CHR$(31)THEN1420:ELSEIFB$=""THEN1420
1440 DB=DB+1:PRINTCHR$(15);
1450 IFB$=CHR$(13)THEN1420
1460 PRINTB$;" ";:IFB$=CHR$(8)THEN1390
1470 A$=A$+B$:IFDB=1THEN1420
1480 PRINTCHR$(15);:IFLEFT$(A$,1)="K"THEN1590
1490 A1$=LEFT$(A$,1):A2$=RIGHT$(A$,1)
1500 C=(ASC(A1$)-48):B=(ASC(A2$)-48)
1510 REM        CHECK IF DOMINO IS VALID                **
1520 FOR A=1TO7
1530 IFP(A)=9 THEN 1550
1540 IF P(A)=C AND P(A+28)=B OR P(A)=B AND P(A+28)=C THEN1690
1550 NEXT A
1560 OUT255,32
1570 PRINT@203,"INVALID                        ";
1580 FOR T=1TO500 : NEXT T : GOTO 1330
1590 REM        PLAYER KNOCKS                           **
1600 FORA=1TO7
1610 IFP(A)=E1ORP(A)=E2ORP(A+28)=E1ORP(A+28)=E2THEN1660
1620 NEXTA
1630 PK(E1)=E1:PK(E2)=E2
1640 PRINT@203,"You knocked                       ";
1650 FOR T=1TO500 : NEXT T :K=K+1:IFK<>1THEN 3040 :ELSE  GOTO 2140
1660 OUT255,32:PRINT@203,"CHEAT ! !  you can play ";P(A);P(A+28);
1670 FORT=1TO500:NEXTT
1680 GOTO1380
1690 IF P(A)=E1 OR P(A)=E2 THEN 1720
1700 IF P(A+28)=E1 OR P(A+28)=E2 THEN 1720
1710 GOTO 1550
1720 L=C : R=B : P(A)=9 : P(A+28)=9
1730 GOSUB 3170
1740 GOTO 1190
1750 REM        DISPLAY PLAYERS DOMINOES                **
1760 PRINT@768,"                                                            ":PRINT@832," ":PRINT@896," ":PRINT@960,"	                                                              ";
1770 PRINT@768,NN$"'s dominoes ";:IFVG=1THENPRINTCHR$(92);"        ";CHR$(91);" Computer's dominoes";
1780 FORA=1TO7
1790 IFP(A)=9THEN1810
1800 PRINT@836+(A-1)*8,A$(P(A));B$(P(A+28));
1810 NEXTA
1820 RETURN
1830 REM       COMPUTER PLAYS FIRST                     **
1840 L=C(A) : R=C(A+28) : C(A)=9 : C(A+28)=9
1850 E1=L :E2=R
1860 GOSUB3170
1870 IFIQ/2<>INT(IQ/2)THENAP=1:GOSUB4150:GOTO1270
1880 REM        CHECK IF COMPUTER WINS                  **
1890 FOR A=1TO7
1900 IF C(A)<>9 THEN 1330
1910 NEXT A
1920 REM        COMPUTER WINS                           **
1930 IFIQ/2<>INT(IQ/2)THENAP=1:GOSUB4150:GOTO1270
1940 FORA=1TO7
1950 IFP(A)=9THEN1970
1960 PC=PC+(P(A)+P(A+28))
1970 NEXTA
1980 GOSUB2750
1990 W1=W1+1
2000 PRINT@267,"I win         Play again ?";
2010 IFDM=1THENFORT=1TO2000:A$=INKEY$:IFA$<>""THEN4220:ELSENEXTT:PRINT"YES";:GOTO460
2020 T=0
2030 A$=INKEY$ :T=T+1:IFT/5=INT(T/5)THENPRINTCHR$(14);:ELSEPRINTCHR$(15);
2040 IFT>1000THEN4220
2050 IFA$=""THEN2030
2060 PRINTCHR$(15);
2070 IF A$="Y" THEN VG=0:GOTO460
2080 IFA$="D"THENVG=1:GOTO460
2090 IFA$<>"N" THEN 2030
2100 REM        DON'T WANT TO PLAY AGAIN                **
2110 NG=NG+1
2120 GOSUB3310
2130 END
2140 REM        PLAYER DID'NT WIN                       **
2150 PA=0
2160 REM        COMPUTERS TURN                          **
2170 IFDM=1THENGOSUB2750
2180 PA=PA+1
2190 PRINT@203,"Thinking                    ";
2200 IFPA=1THENGOSUB1750
2210 REM        CHECK IF COMP. CAN PLAY A DOUBLE        **
2220 FOR A=1TO7
2230 PRINT@212,"*    ";
2240 IFDM=1THENA$=INKEY$:IFA$<>""THEN4220
2250 IF C(A)=9 THEN 2280
2260 IF C(A)=C(A+28) AND C(A)=E1 THEN 2660
2270 IF C(A)=C(A+28) AND C(A)=E2 THEN 2660
2280 NEXT A
2290 REM        CHECK IF COMP. CAN PLAY + FOLLOW        **
2300 FOR A=1TO7
2310 PRINT@212," *  ";
2320 IFDM=1THENA$=INKEY$:IFA$<>""THEN4220
2330 IF C(A)=9 THEN 2380
2340 IF C(A)=E1 AND(PK(E1)=9 ORPA<>1) THEN H=C(A+28):IF(PK(H)<>9 OR PA=3)THENR=1:GOTO2560
2350 IF C(A)=E2 AND(PK(E2)=9 OR PA<>1)THEN H=C(A+28) :IF(PK(H)<>9 OR PA=3)THENR=2:GOTO2560
2360 IFC(A+28)=E1 AND(PK(E1)=9 OR PA<>1)THEN H=C(A) :IF(PK(H)<>9OR PA=3)THENR=3 :GOTO2560
2370 IF C(A+28)=E2 AND(PK(E2)=9 OR PA<>1)THEN H=C(A) :IF(PK(H)<>9 OR PA=3)THEN R=4 :GOTO 2560
2380 NEXT A
2390 REM        CHECK IF COMP. CAN PLAY                 **
2400 FOR A=1TO7
2410 PRINT@212,"  * ";
2420 IFDM=1THENA$=INKEY$:IFA$<>""THEN4220
2430 IF C(A)=9 THEN 2480
2440 IFC(A)=E1 AND(PK(E1)=9 ORPA<>1)THEN H=C(A+28):IF(PK(H)<>9 OR PA=3)THEN2660
2450 IFC(A)=E2 AND(PK(E2)=9 OR PA<>1) THEN H=C(A+28):IF(PK(H)<>9 ORPA=3)THEN2660
2460 IFC(A+28)=E1 AND(PK(E1)=9 OR PA<>1) THEN H=C(A):IF(PK(H)<>9ORPA=3)THEN2660
2470 IFC(A+28)=E2 AND(PK(E2)=9 OR PA<>1) THEN H=C(A):IF(PK(H)<>9 OR PA=3)THEN2660
2480 NEXT A
2490 IFPA<3THEN2180
2500 REM        COMPUTER IS KNOCKING                    **
2510 PA=0
2520 PRINT@203,"I am knocking               ";
2530 K=K+1 :IF K<>1 THEN 3040
2540 FOR T=1TO500 :NEXT T
2550 GOTO 1330
2560 REM        CHECK IF COMP.CAN FOLLOW OWN PLAY       **
2570 FOR I=1TO7
2580 PRINT@212,"   *";
2590 IFDM=1THENA$=INKEY$:IFA$<>""THEN4220
2600 IF C(A)=C(I) AND C(A+28)=C(I+28) THEN 2640
2610 IFC(I)=9 THEN 2640
2620 IF C(I)=H THEN 2660
2630 IF C(I+28)=H THEN 2660
2640 NEXT I
2650 ONR GOTO 2350,2360,2370,2380
2660 REM        CHOOSE SIDE COMP. PLAYS ON              **
2670 PA=0
2680 PRINT@203,"I play ";C(A);C(A+28)"  ";:FORT=1TO200:NEXTT
2690 IFC(A)=E1ANDC(A+28)=E2THENGOSUB2830
2700 IFC(A+28)=E1ANDC(A)=E2THENGOSUB2830
2710 IF C(A)=E1 OR C(A+28)=E2 THEN L=C(A):R=C(A+28) :C(A)=9 :C(A+28)=9 :GOTO2730
2720 IF C(A)=E2 OR C(A+28)=E1 THEN L=C(A+28) :R=C(A) :C(A)=9 :C(A+28)=9
2730 GOSUB 3170
2740 GOTO1880
2750 REM        DISPLAY COMPUTERS DOMINOES              **
2760 IF VG<>1THEN RETURN
2770 PRINT@576,"                                                            ":PRINT@640,"                                                            ":PRINT@704,"                                                         "
2780 FOR J=1TO7
2790 IF C(J)=9 THEN 2810
2800 PRINT@572+J*8,A$(C(J));B$(C(J+28));
2810 NEXT J
2820 RETURN
2830 REM        COMP CAN PLAY BOTH SIDES                        
2840 FORI=1TO7
2850 IFC(I)=9THEN2890
2860 IFC(A)=C(I)ANDC(A+28)=C(I+28)THEN2890
2870 IF(C(I)ORC(I+28))=C(A+28)THENRETURN
2880 IFC(I)=C(A) OR C(I+28)=C(A) THENT1=C(A):C(A)=C(A+28):C(A+28)=T1:RETURN
2890 NEXTI
2900 FORI=1TO7
2910 IFC(I)=9THEN2940
2920 IFC(A)=C(I)ANDC(A+28)=C(I+28)THEN2940
2930 IFPK(C(A))<>9THENT1=C(A):C(A)=C(A+28):C(A+28)=T1:RETURN
2940 NEXTI
2950 RETURN
2960 REM        SORT COMPS.DOMS. HIGHEST FIRST          **
2970 X=7
2980 FOR A=1TOX
2990 IFC(A)+C(A+28)<C(X)+C(X+28)THEN T1=C(A):T2=C(A+28):C(A)=C(X):C(A+28)=C(X+28):C(X)=T1:C(X+28)=T2
3000 IFIQ=0THENZ=RND(7)-1:Y=RND(7)-1:PRINT@12,A$(Z);B$(Y)
3010 NEXT
3020 IFX=1THENRETURN
3030 X=X-1:GOTO2980
3040 PRINT@203,"We are both knocking  ";:PRINT@268,"so it is up to a count";
3050 VG=1
3060 FOR T=1TO1000 : NEXT T : GOSUB 2750 : GOSUB 1750
3070 FOR A=1TO7
3080 IF C(A)=9 THEN 3100
3090 CC=CC+(C(A)+C(A+28))
3100 IFP(A)=9THEN 3120
3110 PC=PC+(P(A)+P(A+28))
3120 NEXT A
3130 PRINT@203,"I have ";CC;"   You have ";PC;
3140 IF CC>PC THEN 1310
3150 IFCC=PC THEN W3=W3+1:PRINT@268,"It is a draw   Play again  ?";:GOTO 2010
3160 GOTO 1990
3170 REM       O/P TO SCREEN + END SORT                **
3180 K=0
3190 IF L=E1 THEN E1=R :GOTO 3230
3200 IF L=E2 THEN E2=R :GOTO 3270
3210 IF R=E1 THEN E1=L :L=R:R=E1:GOTO 3230
3220 IF R=E2 THEN E2=L :L=R:R=E2:GOTO 3270
3230 IFII=0ANDLP>3THENPRINT@LP,A$(R);B$(L);:LP=LP-8:RETURN
3240 IFLP<4THENII=II+1:IFII=1THENPRINT@132,C$(L);D$(R);:RETURN
3250 IFIY<7THENPRINT@388+IY*8,A$(L);B$(R);:IY=IY+1:RETURN
3260 PRINT@183,C$(R);D$(L);:FORT=108TO120:RESET(T,6):NEXTT:RETURN
3270 IFIZ=0ANDRP<59THENPRINT@RP,A$(L);B$(R);:RP=RP+8:RETURN
3280 IFRP=>59THENIZ=IZ+1:IFIZ=1THENPRINT@183,C$(L);D$(R);:SET(110,6):RESET(119,6):RETURN
3290 IFIX<7THENPRINT@436-IX*8,A$(R);B$(L);:IX=IX+1:RETURN
3300 PRINT@132,C$(R);D$(L);:RETURN
3310 REM  PLAY COUNT ROUTINE                              **
3320 X1=X1+CC:X2=X2+PC:T=0
3330 IFNG=1THENS$="game  ":ELSES$="games"
3340 CLS:PRINTCHR$(23)
3350 PRINT"So far out of ";NG;S$;" 
the scores are :-"
3360 PRINT"COMPUTERS total count =";X1
3370 PRINTNN$;"'s  total count =";X2
3380 IFW1=1THENS$="game":ELSES$="games"
3390 PRINT"The computer has won ";W1;S$
3400 IFW2=1THENS$="game":ELSES$="games"
3410 PRINT"   You have won      ";W2;S$
3420 IFW3=1THENS$="game ":ELSES$="games"
3430 PRINT"There have been ";W3;S$;" drawn"
3440 PRINT:PRINT"PRESS A KEY TO CONTINUE";
3450 T=0
3460 Q$=INKEY$:T=T+1:IFT/5=INT(T/5)THENPRINTCHR$(14);:ELSEPRINTCHR$(15);
3470 IFDM=1ANDT=200THENRETURN
3480 IFT>500THEN4220
3490 IFQ$=""THEN3460
3500 IFDM=1THENPRINTCHR$(15):GOTO4220
3510 PRINTCHR$(15);
3520 RETURN
3530 REM      SET UP GRAPHICS                         **
3540 A$=CHR$(131)+CHR$(131)+CHR$(131)+CHR$(129)
3550 B$=CHR$(130)+CHR$(131)+CHR$(131)+CHR$(131)
3560 C$=CHR$(26)+CHR$(24)+CHR$(24)+CHR$(24)+CHR$(24)
3570 FORA=0TO6
3580 FORB=1TO2
3590 FORC=1TO4
3600 READ D,E
3610 A$(A)=A$(A)+CHR$(128+D):B$(A)=B$(A)+CHR$(128+E)
3620 NEXTC
3630 A$(A)=A$(A)+C$:B$(A)=B$(A)+C$
3640 NEXTB
3650 A$(A)=A$(A)+B$+CHR$(27)+CHR$(27):B$(A)=B$(A)+A$
3660 PRINT@A*8+4,A$(A);B$(A);
3670 NEXTA
3680 A$=CHR$(26)+CHR$(24)+CHR$(24)+CHR$(24)+CHR$(24)+CHR$(24)
3690 B$=CHR$(154)+CHR$(143)+CHR$(143)+CHR$(143)+CHR$(147)
3700 FORA=0TO6
3710 C$(A)=B$+A$
3720 FORB=1TO5
3730 READ C
3740 C$(A)=C$(A)+CHR$(128+C)
3750 NEXTB
3760 F=RND(7)-1:G=RND(7)-1
3770 PRINT@4+A*8,A$(F);B$(G);
3780 C$(A)=C$(A)+A$
3790 NEXTA
3800 FORA=0TO6
3810 FORB=1TO2
3820 FORD=1TO5
3830 READ C
3840 D$(A)=D$(A)+CHR$(128+C)
3850 NEXTD
3860 D$(A)=D$(A)+A$
3870 F=RND(7)-1:G=RND(7)-1
3880 PRINT@4+A*8,A$(F);B$(G);
3890 NEXTB
3900 NEXTA
3910 RETURN
3920 REM       DATA FOR GRAPHICS                       **
3930 DATA22,3,3,3,3,3,3,41
3940 DATA21,0,0,0,0,0,0,42
3950 DATA22,3,3,3,3,3,3,41
3960 DATA21,0,0,2,1,0,0,42
3970 DATA22,35,19,3,3,3,3,41
3980 DATA21,0,0,0,0,8,4,42
3990 DATA22,35,19,3,3,3,3,41
4000 DATA21,0,0,2,1,8,4,42
4010 DATA22,35,19,3,3,35,19,41
4020 DATA21,8,4,0,0,8,4,42
4030 DATA22,35,19,3,3,35,19,41
4040 DATA21,8,4,2,1,8,4,42
4050 DATA22,35,19,35,19,35,19,41
4060 DATA21,8,4,8,4,8,4,42
4070 DATA21,0,0,0,21,21,0,4,0,21,21,1,0,16,21,21,1,4,16,21,21,17,0,17,21,21,17,4,17,21,21,17,17,17,21
4080 DATA21,0,0,0,21,37,48,48,48,5
4090 DATA21,0,16,0,21,37,48,48,48,5
4100 DATA21,4,0,0,21,37,48,48,49,5
4110 DATA21,4,16,0,21,37,48,48,49,5
4120 DATA21,4,0,4,21,37,49,48,49,5
4130 DATA21,4,16,4,21,37,49,48,49,5
4140 DATA21,4,4,4,21,37,49,49,49,5
4150 FORA=1TO7
4160 IQ=IQ+1
4170 T1=C(A):T2=C(A+28):C(A)=P(A):C(A+28)=P(A+28):P(A)=T1:P(A+28)=T2
4180 NEXTA
4190 IFIQ=7THENGOSUB2960
4200 IFAP=1THENRETURN
4210 GOTO2160
4220 NN$="":T=-5000:CLS:FORA=0TO6:PRINT@A*8,A$(A);B$(RND(7)-1);:NEXT:GOTO80
4230 REM     ALL DONE                                   **
